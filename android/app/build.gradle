plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

// Conditionally apply Google Services plugin only if google-services.json exists and is valid
def googleServicesFile = file("google-services.json")
if (googleServicesFile.exists() && googleServicesFile.length() > 0) {
    apply plugin: 'com.google.gms.google-services'
    println("Google Services plugin applied - Firebase enabled")
} else {
    println("Google Services plugin skipped - google-services.json not found or empty")
    println("Firebase/FCM will not work until google-services.json is configured")
}

// Load keystore properties from key.properties file
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace = "com.nextpital.prodoc"
    compileSdk = flutter.compileSdkVersion
  //  compileSdk = 35

    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
        // Enable core library desugaring for flutter_local_notifications
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "com.nextpital.prodoc"
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
       // targetSdk = 35

        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        release {
            // Use release signing config if keystore properties exist, otherwise use debug
            signingConfig = keystorePropertiesFile.exists() ? signingConfigs.release : signingConfigs.debug
            
            // Enable code shrinking, obfuscation, and optimization
            // ProGuard rules have been updated to keep Google Play Core classes
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

flutter {
    source = "../.."
}

dependencies {
    // Updated Google Play Core for SDK 34 compatibility
    implementation 'com.google.android.play:app-update:2.1.0'
    implementation 'com.google.android.play:review:2.0.1'
    
    // Add androidx.startup dependency to fix ClassNotFoundException
    implementation 'androidx.startup:startup-runtime:1.1.1'
    
    // Core library desugaring for flutter_local_notifications
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.4'
}
